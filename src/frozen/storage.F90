#include "global.h"

module storage_m

  use global_m
  use messages_m
  use profiling_m

#if defined(HAVE_MPI)
  use boundaries_m, only: dvec_ghost_update
#endif
  use grid_m,       only: grid_t
  use kinds_m,      only: wp, operator(.equal.)
  use mesh_m,       only: mesh_t

  use simulation_m, only: &
    simulation_t,         &
    simulation_get

  use intrpl_m, only: &
    intrpl_init

  use intrpl_m, only: &
    intrpl_t,         &
    intrpl_eval,      &
    intrpl_copy,      &
    intrpl_end

  use intrpl_m, only:               &
    STORAGE_INTRPL_OK => INTRPL_OK, &
    STORAGE_INTRPL_OD => INTRPL_OD, &
    STORAGE_INTRPL_NI => INTRPL_NI

  implicit none

  private
  public ::                &
    storage_init,          &
    storage_start,         &
    storage_update,        &
    storage_stop,          &
    storage_reset,         &
    storage_accumulate,    &
    storage_eval,          &
    storage_get,           &
    storage_get_size,      &
    storage_get_dimension, &
    storage_copy,          &
    storage_end

  public ::            &
    STORAGE_INTRPL_OK, &
    STORAGE_INTRPL_OD, &
    STORAGE_INTRPL_NI

  type, public :: storage_t
    private
    type(simulation_t),                pointer :: sim     =>null()
    type(mesh_t),                      pointer :: mesh    =>null()
    logical                                    :: alloc   = .true.
    logical                                    :: full    = .true.
    logical                                    :: fine    = .false.
    integer                                    :: ndim    = 0
    real(kind=wp)                              :: default = 0.0_wp
    real(kind=wp), dimension(:,:), allocatable :: data
  end type storage_t

  type, public :: storage_intrpl_t
    private
    type(storage_t), pointer :: self =>null()
    type(intrpl_t)           :: intrp
  end type storage_intrpl_t

  interface storage_init
    module procedure storage_init_simple
    module procedure storage_init_copy
    module procedure storage_intrpl_init
  end interface storage_init

  interface storage_eval
    module procedure storage_intrpl_eval_1d
    module procedure storage_intrpl_eval_md
  end interface storage_eval

  interface storage_get
    module procedure storage_get_sim
    module procedure storage_get_storage_1d
    module procedure storage_get_storage_md
  end interface storage_get

  interface storage_copy
    module procedure storage_copy_storage
    module procedure storage_intrpl_copy
  end interface storage_copy

  interface storage_end
    module procedure storage_end_storage
    module procedure storage_intrpl_end
  end interface storage_end

contains

  ! ---------------------------------------------------------
  subroutine storage_init_simple(this, ndim, default, full, allocate)
    type(storage_t),         intent(out) :: this
    integer,       optional, intent(in)  :: ndim
    real(kind=wp), optional, intent(in)  :: default
    logical,       optional, intent(in)  :: full
    logical,       optional, intent(in)  :: allocate
    !
    PUSH_SUB(storage_init_simple)
    nullify(this%sim, this%mesh)
    this%full=.true.
    if(present(full))this%full=full
    this%alloc=.true.
    if(present(allocate))this%alloc=allocate
    this%ndim=1
    if(present(ndim))then
      ASSERT(ndim>0)
      this%ndim=ndim
    end if
    this%default=0.0_wp
    if(present(default))this%default=default
    POP_SUB(storage_init_simple)
    return
  end subroutine storage_init_simple

  ! ---------------------------------------------------------
  subroutine storage_init_copy(this, that)
    type(storage_t), intent(out) :: this
    type(storage_t), intent(in)  :: that
    !
    PUSH_SUB(storage_init_copy)
    ASSERT(associated(that%sim))
    ASSERT(associated(that%mesh))
    ASSERT(that%ndim>0)
    call storage_init_simple(this, that%ndim, that%default, that%full, that%alloc)
    call storage_start(this, that%sim, that%fine)
    POP_SUB(storage_init_copy)
    return
  end subroutine storage_init_copy

  ! ---------------------------------------------------------
  subroutine storage_start(this, sim, fine)
    type(storage_t),            intent(inout) :: this
    type(simulation_t), target, intent(in)    :: sim
    logical,          optional, intent(in)    :: fine
    !
    type(grid_t), pointer :: grid
    !
    PUSH_SUB(storage_start)
    nullify(grid)
    ASSERT(.not.associated(this%sim))
    ASSERT(.not.associated(this%mesh))
    ASSERT(this%ndim>0)
    ASSERT(.not.allocated(this%data))
    this%sim=>sim
    this%fine=.false.
    if(present(fine))this%fine=fine
    call simulation_get(sim, grid)
    ASSERT(associated(grid))
    if(this%fine)then
      this%mesh=>grid%fine%mesh
    else
      this%mesh=>grid%mesh
    end if
    nullify(grid)
    ASSERT(this%mesh%np>0)
    ASSERT(this%mesh%np_part>0)
    ASSERT(this%mesh%np<this%mesh%np_part)
    if(this%alloc)then
      if(this%full)then
        SAFE_ALLOCATE(this%data(this%mesh%np_part,this%ndim))
      else
        SAFE_ALLOCATE(this%data(this%mesh%np,this%ndim))
      end if
    end if
    call storage_reset(this)
    POP_SUB(storage_start)
    return
  end subroutine storage_start

  ! ---------------------------------------------------------
  subroutine storage_update(this)
    type(storage_t), intent(inout) :: this
    !
    integer :: indx
    !
    PUSH_SUB(storage_update)
    ASSERT(associated(this%sim))
    ASSERT(associated(this%mesh))
    ASSERT(this%ndim>0)
    if(this%alloc)then
      if(this%full)then
        do indx=this%mesh%np+1, size(this%data,dim=1)
          this%data(indx,:)=0.0_wp
        end do
      end if
#if defined(HAVE_MPI)
      do indx = 1, this%ndim
        call dvec_ghost_update(this%mesh%vp, this%data(:,indx))
      end do
#endif
    end if
    POP_SUB(storage_update)
    return
  end subroutine storage_update

  ! ---------------------------------------------------------
  subroutine storage_stop(this)
    type(storage_t), intent(inout) :: this
    !
    PUSH_SUB(storage_stop)
    ASSERT(associated(this%sim))
    ASSERT(associated(this%mesh))
    ASSERT(this%ndim>0)
    if(this%alloc)then
      SAFE_DEALLOCATE_A(this%data)
    end if
    POP_SUB(storage_stop)
    return
  end subroutine storage_stop

  ! ---------------------------------------------------------
  subroutine storage_reset(this, default)
    type(storage_t),         intent(inout) :: this
    real(kind=wp), optional, intent(in)    :: default
    !
    real(kind=wp) :: dflt
    !
    PUSH_SUB(storage_reset)
    ASSERT(associated(this%sim))
    ASSERT(associated(this%mesh))
    ASSERT(this%ndim>0)
    if(this%alloc)then
      dflt=this%default
      if(present(default))dflt=default
      this%data(1:this%mesh%np,:)=dflt
      call storage_update(this)
    end if
    POP_SUB(storage_reset)
    return
  end subroutine storage_reset

  ! ---------------------------------------------------------
  subroutine storage_accumulate(this, that)
    type(storage_t), intent(inout) :: this
    type(storage_t), intent(in)    :: that
    !
    integer :: indx
    !
    PUSH_SUB(storage_accumulate)
    ASSERT(associated(this%sim))
    ASSERT(associated(that%sim))
    ASSERT(associated(this%mesh))
    ASSERT(associated(that%mesh))
    ASSERT(associated(this%mesh,that%mesh))
    ASSERT(this%ndim>0)
    ASSERT(that%ndim>0)
    ASSERT(this%ndim==that%ndim)
    ASSERT(this%default.equal.that%default)
    if(this%alloc.and.that%alloc)then
      do indx = 1, this%mesh%np
        this%data(indx,:)=this%data(indx,:)+that%data(indx,:)
      end do
      call storage_update(this)
    end if
    POP_SUB(storage_accumulate)
    return
  end subroutine storage_accumulate

  ! ---------------------------------------------------------
  elemental function storage_get_size(this) result(that)
    type(storage_t), intent(in) :: this
    !
    integer :: that
    !
    that=0
    if(associated(this%mesh))&
      that=this%mesh%np
    return
  end function storage_get_size

  ! ---------------------------------------------------------
  elemental function storage_get_dimension(this) result(that)
    type(storage_t), intent(in) :: this
    !
    integer :: that
    !
    that=0
    if(this%ndim>0)&
      that=this%ndim
    return
  end function storage_get_dimension

  ! ---------------------------------------------------------
  subroutine storage_get_sim(this, that)
    type(storage_t),     target, intent(in) :: this
    type(simulation_t), pointer             :: that
    !
    PUSH_SUB(storage_get_sim)
    nullify(that)
    if(associated(this%sim))&
      that=>this%sim
    POP_SUB(storage_get_sim)
    return
  end subroutine storage_get_sim

  ! ---------------------------------------------------------
  subroutine storage_get_storage_1d(this, that)
    type(storage_t),              target, intent(in) :: this
    real(kind=wp), dimension(:), pointer             :: that
    !
    PUSH_SUB(storage_get_storage_1d)
    nullify(that)
    if(allocated(this%data))then
      ASSERT(this%alloc)
      ASSERT(this%ndim==1)
      that=>this%data(:,1)
    end if
    POP_SUB(storage_get_storage_1d)
    return
  end subroutine storage_get_storage_1d

  ! ---------------------------------------------------------
  subroutine storage_get_storage_md(this, that)
    type(storage_t),                target, intent(in) :: this
    real(kind=wp), dimension(:,:), pointer             :: that
    !
    PUSH_SUB(storage_get_storage_md)
    nullify(that)
    if(allocated(this%data))then
      ASSERT(this%alloc)
      ASSERT(this%ndim>0)
      that=>this%data
    end if
    POP_SUB(storage_get_storage_md)
    return
  end subroutine storage_get_storage_md

  ! ---------------------------------------------------------
  subroutine storage_copy_storage(this, that)
    type(storage_t), intent(inout) :: this
    type(storage_t), intent(in)    :: that
    !
    PUSH_SUB(storage_copy_storage)
    call storage_end(this)
    if(that%ndim>0)then
      call storage_init_simple(this, that%ndim, that%default, that%full, that%alloc)
      if(associated(that%sim).and.associated(that%mesh))then
        call storage_start(this, that%sim, that%fine)
        if(that%alloc)then
          this%data(1:this%mesh%np,:)=that%data(1:this%mesh%np,:)
          call storage_update(this)
        end if
      end if
    end if
    POP_SUB(storage_copy_storage)
    return
  end subroutine storage_copy_storage

  ! ---------------------------------------------------------
  subroutine storage_end_storage(this)
    type(storage_t), intent(inout) :: this
    !
    PUSH_SUB(storage_end_storage)
    nullify(this%sim, this%mesh)
    this%full=.true.
    this%fine=.false.
    this%ndim=0
    this%default=0.0_wp
    if(this%alloc)then
      SAFE_DEALLOCATE_A(this%data)
    end if
    this%alloc=.true.
    POP_SUB(storage_end_storage)
    return
  end subroutine storage_end_storage

  ! ---------------------------------------------------------
  subroutine storage_intrpl_init(this, that, type)
    type(storage_intrpl_t),  intent(out) :: this
    type(storage_t), target, intent(in)  :: that
    integer,       optional, intent(in)  :: type
    !
    PUSH_SUB(storage_intrpl_init)
    nullify(this%self)
    if(allocated(that%data))then
      ASSERT(that%alloc)
      this%self=>that
      call intrpl_init(this%intrp, that%sim, that%data, type=type, default=that%default)
    end if
    POP_SUB(storage_intrpl_init)
    return
  end subroutine storage_intrpl_init

  ! ---------------------------------------------------------
  subroutine storage_intrpl_eval_1d(this, x, val, ierr)
    type(storage_intrpl_t),       intent(in)  :: this
    real(kind=wp),  dimension(:), intent(in)  :: x
    real(kind=wp),                intent(out) :: val
    integer,                      intent(out) :: ierr
    !
    PUSH_SUB(storage_intrpl_eval_1d)
    ierr=STORAGE_INTRPL_NI
    if(associated(this%self))&
      call intrpl_eval(this%intrp, x, val, ierr)
    POP_SUB(storage_intrpl_eval_1d)
    return
  end subroutine storage_intrpl_eval_1d

  ! ---------------------------------------------------------
  subroutine storage_intrpl_eval_md(this, x, val, ierr)
    type(storage_intrpl_t),       intent(in)  :: this
    real(kind=wp),  dimension(:), intent(in)  :: x
    real(kind=wp),  dimension(:), intent(out) :: val
    integer,                      intent(out) :: ierr
    !
    PUSH_SUB(storage_intrpl_eval_md)
    ierr=STORAGE_INTRPL_NI
    if(associated(this%self))&
      call intrpl_eval(this%intrp, x, val, ierr)
    POP_SUB(storage_intrpl_eval_md)
    return
  end subroutine storage_intrpl_eval_md

  ! ---------------------------------------------------------
  subroutine storage_intrpl_copy(this, that)
    type(storage_intrpl_t), intent(inout) :: this
    type(storage_intrpl_t), intent(in)    :: that
    !
    PUSH_SUB(storage_intrpl_copy)
    call storage_intrpl_end(this)
    if(associated(that%self))then
      this%self=>that%self
      call intrpl_copy(this%intrp, that%intrp)
    end if
    POP_SUB(storage_intrpl_copy)
    return
  end subroutine storage_intrpl_copy

  ! ---------------------------------------------------------
  subroutine storage_intrpl_end(this)
    type(storage_intrpl_t), intent(inout) :: this
    !
    PUSH_SUB(storage_intrpl_end)
    if(associated(this%self))&
      call intrpl_end(this%intrp)
    nullify(this%self)
    POP_SUB(storage_intrpl_end)
    return
  end subroutine storage_intrpl_end

end module storage_m

!! Local Variables:
!! mode: f90
!! End:
