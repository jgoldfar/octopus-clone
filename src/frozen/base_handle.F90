#include "global.h"

#undef LIST_TEMPLATE_NAME
#undef LIST_TYPE_NAME
#undef LIST_TYPE_MODULE_NAME

#undef DICT_TEMPLATE_NAME
#undef DICT_TYPE_NAME
#undef DICT_TYPE_MODULE_NAME
#undef DICT_INCLUDE_PREFIX
#undef DICT_INCLUDE_HEADER
#undef DICT_INCLUDE_BODY

module base_handle_oct_m

  use base_density_oct_m
  use base_geometry_oct_m
  use base_hamiltonian_oct_m
  use base_model_oct_m
  use base_states_oct_m
  use base_system_oct_m
  use geometry_oct_m
  use global_oct_m
  use grid_oct_m
  use json_oct_m
  use messages_oct_m
  use profiling_oct_m
  use simulation_oct_m
  use space_oct_m

#define LIST_TEMPLATE_NAME base_handle
#define LIST_INCLUDE_PREFIX
#include "tlist_inc.F90"
#undef LIST_INCLUDE_PREFIX
#undef LIST_TEMPLATE_NAME

#define DICT_TEMPLATE_NAME base_handle
#define DICT_INCLUDE_PREFIX
#include "tdict_inc.F90"
#undef DICT_INCLUDE_PREFIX
#undef DICT_TEMPLATE_NAME

#define TEMPLATE_PREFIX base_handle
#define INCLUDE_PREFIX
#include "iterator_inc.F90"
#undef INCLUDE_PREFIX
#undef TEMPLATE_PREFIX

  implicit none

  private

  public ::         &
    HNDL_TYPE_NONE

  public ::                  &
    BASE_HANDLE_OK,          &
    BASE_HANDLE_KEY_ERROR,   &
    BASE_HANDLE_EMPTY_ERROR

  public ::        &
    base_handle_t

  public ::                &
    base_handle__init__,   &
    base_handle__start__,  &
    base_handle__update__, &
    base_handle__stop__,   &
    base_handle__reset__,  &
    base_handle__copy__,   &
    base_handle__end__

  public ::             &
    base_handle_new,    &
    base_handle_del,    &
    base_handle_init,   &
    base_handle_start,  &
    base_handle_update, &
    base_handle_stop,   &
    base_handle_sets,   &
    base_handle_gets,   &
    base_handle_get,    &
    base_handle_copy,   &
    base_handle_end

#define LIST_TEMPLATE_NAME base_handle
#define LIST_INCLUDE_HEADER
#include "tlist_inc.F90"
#undef LIST_INCLUDE_HEADER
#undef LIST_TEMPLATE_NAME

#define DICT_TEMPLATE_NAME base_handle
#define DICT_INCLUDE_HEADER
#include "tdict_inc.F90"
#undef DICT_INCLUDE_HEADER
#undef DICT_TEMPLATE_NAME

  integer, parameter :: HNDL_TYPE_NONE = 0

  integer, parameter :: BASE_HANDLE_OK          = BASE_HANDLE_DICT_OK
  integer, parameter :: BASE_HANDLE_KEY_ERROR   = BASE_HANDLE_DICT_KEY_ERROR
  integer, parameter :: BASE_HANDLE_EMPTY_ERROR = BASE_HANDLE_DICT_EMPTY_ERROR

  type :: base_handle_t
    private
    type(json_object_t), pointer :: config =>null()
    type(base_handle_t), pointer :: prnt   =>null()
    integer                      :: type   = HNDL_TYPE_NONE
    type(simulation_t)           :: sim
    type(base_model_t)           :: model
    type(base_handle_dict_t)     :: dict
    type(base_handle_list_t)     :: list
  end type base_handle_t

  interface base_handle__init__
    module procedure base_handle__init__begin
    module procedure base_handle__init__build
    module procedure base_handle__init__finish
    module procedure base_handle__init__copy
  end interface base_handle__init__

  interface base_handle__copy__
    module procedure base_handle__copy__begin
    module procedure base_handle__copy__finish
  end interface base_handle__copy__

  interface base_handle_init
    module procedure base_handle_init_type
    module procedure base_handle_init_pass
    module procedure base_handle_init_copy
  end interface base_handle_init

  interface base_handle_gets
    module procedure base_handle_gets_name
  end interface base_handle_gets

  interface base_handle_get
    module procedure base_handle_get_info
    module procedure base_handle_get_config
    module procedure base_handle_get_simulation
    module procedure base_handle_get_geom
    module procedure base_handle_get_density
    module procedure base_handle_get_states
    module procedure base_handle_get_system
    module procedure base_handle_get_hamiltonian
    module procedure base_handle_get_model
  end interface base_handle_get

  interface base_handle_copy
    module procedure base_handle_copy_type
  end interface base_handle_copy

  interface base_handle_end
    module procedure base_handle_end_type
  end interface base_handle_end

#define TEMPLATE_PREFIX base_handle
#define INCLUDE_HEADER
#include "iterator_inc.F90"
#undef INCLUDE_HEADER
#undef TEMPLATE_PREFIX

contains

#define LIST_TEMPLATE_NAME base_handle
#define LIST_INCLUDE_BODY
#include "tlist_inc.F90"
#undef LIST_INCLUDE_BODY
#undef LIST_TEMPLATE_NAME

#define DICT_TEMPLATE_NAME base_handle
#define DICT_INCLUDE_BODY
#include "tdict_inc.F90"
#undef DICT_INCLUDE_BODY
#undef DICT_TEMPLATE_NAME

  ! ---------------------------------------------------------
  subroutine base_handle__new__(this)
    type(base_handle_t), pointer :: this

    PUSH_SUB(base_handle__new__)

    nullify(this)
    SAFE_ALLOCATE(this)

    POP_SUB(base_handle__new__)
  end subroutine base_handle__new__

  ! ---------------------------------------------------------
  subroutine base_handle__del__(this)
    type(base_handle_t), pointer :: this

    PUSH_SUB(base_handle__del__)

    if(associated(this))then
      SAFE_DEALLOCATE_P(this)
    end if
    nullify(this)

    POP_SUB(base_handle__del__)
  end subroutine base_handle__del__

  ! ---------------------------------------------------------
  subroutine base_handle_new(this, that)
    type(base_handle_t),  intent(inout) :: this
    type(base_handle_t), pointer        :: that

    PUSH_SUB(base_handle_new)

    nullify(that)
    call base_handle__new__(that)
    call base_handle__set__(that, this)
    call base_handle_list_push(this%list, that)

    POP_SUB(base_handle_new)
  end subroutine base_handle_new

  ! ---------------------------------------------------------
  subroutine base_handle_del(this)
    type(base_handle_t), pointer :: this

    PUSH_SUB(base_handle_del)

    if(associated(this))then
      if(associated(this%prnt))then
        call base_handle_list_del(this%prnt%list, this)
        call base_handle_end(this)
        call base_handle__del__(this)
      end if
    end if

    POP_SUB(base_handle_del)
  end subroutine base_handle_del

  ! ---------------------------------------------------------
  subroutine base_handle__init__begin(this, config)
    type(base_handle_t),         intent(out) :: this
    type(json_object_t), target, intent(in)  :: config

    type(json_object_t), pointer :: cnfg
    integer                      :: ierr

    PUSH_SUB(base_handle__init__begin)

    nullify(cnfg)
    this%config => config
    call json_get(this%config, "type", this%type, ierr)
    if(ierr/=JSON_OK) this%type = HNDL_TYPE_NONE
    call json_get(this%config, "model", cnfg, ierr)
    ASSERT(ierr==JSON_OK)
    call base_model__init__(this%model, cnfg)
    nullify(cnfg)
    call base_handle_dict_init(this%dict)
    call base_handle_list_init(this%list)
    
    POP_SUB(base_handle__init__begin)
  end subroutine base_handle__init__begin

  ! ---------------------------------------------------------
  subroutine base_handle__init__build(this, config, handle_init)
    type(base_handle_t), intent(inout) :: this
    type(json_object_t), intent(in)    :: config

    type(json_object_iterator_t)        :: iter
    character(len=BASE_HANDLE_NAME_LEN) :: name
    type(json_object_t),        pointer :: cnfg
    type(base_handle_t),        pointer :: hndl
    integer                             :: ierr

    interface
      subroutine handle_init(this, config)
        use json_oct_m
        import :: base_handle_t
        type(base_handle_t), intent(out) :: this
        type(json_object_t), intent(in)  :: config
      end subroutine handle_init
    end interface

    PUSH_SUB(base_handle__init__build)

    call json_init(iter, config)
    do
      nullify(cnfg, hndl)
      call json_next(iter, name, cnfg, ierr)
      if(ierr/=JSON_OK)exit
      call base_handle_new(this, hndl)
      call handle_init(hndl, cnfg)
      call base_handle__set__(hndl, this)
      call base_handle_sets(this, name, hndl)
    end do
    call json_end(iter)
    nullify(cnfg, hndl)

    POP_SUB(base_handle__init__build)
  end subroutine base_handle__init__build

  ! ---------------------------------------------------------
  subroutine base_handle__init__finish(this)
    type(base_handle_t), intent(inout) :: this

    type(json_object_t), pointer :: cnfg
    type(space_t),       pointer :: space
    type(geometry_t),    pointer :: geo
    integer                      :: ierr

    PUSH_SUB(base_handle__init__finish)

    nullify(cnfg, space, geo)
    call base_model__init__(this%model)
    call base_model_get(this%model, space)
    ASSERT(associated(space))
    call base_model_get(this%model, geo)
    ASSERT(associated(geo))
    call json_get(this%config, "simulation", cnfg, ierr)
    ASSERT(ierr==JSON_OK)
    call simulation_init(this%sim, geo, space, cnfg)
    nullify(cnfg, space, geo)

    POP_SUB(base_handle__init__finish)
  end subroutine base_handle__init__finish

  ! ---------------------------------------------------------
  subroutine base_handle__init__copy(this, that)
    type(base_handle_t), intent(out) :: this
    type(base_handle_t), intent(in)  :: that

    PUSH_SUB(base_handle__init__copy)

    ASSERT(associated(that%config))
    call base_handle__init__(this, that%config)
    if(simulation_assoc(that%sim))then
      call simulation_copy(this%sim, that%sim)
      call base_model__start__(this%model, this%sim)
    end if

    POP_SUB(base_handle__init__copy)
  end subroutine base_handle__init__copy

  ! ---------------------------------------------------------
  recursive subroutine base_handle_init_type(this, config)
    type(base_handle_t), intent(out) :: this
    type(json_object_t), intent(in)  :: config

    PUSH_SUB(base_handle_init_type)

    call base_handle_init_pass(this, config, base_handle_init_type)

    POP_SUB(base_handle_init_type)
  end subroutine base_handle_init_type

  ! ---------------------------------------------------------
  recursive subroutine base_handle_init_pass(this, config, handle_init)
    type(base_handle_t), intent(out) :: this
    type(json_object_t), intent(in)  :: config

    interface
      subroutine handle_init(this, config)
        use json_oct_m
        import :: base_handle_t
        type(base_handle_t), intent(out) :: this
        type(json_object_t), intent(in)  :: config
      end subroutine handle_init
    end interface

    type(json_object_t), pointer :: dict
    integer                      :: ierr

    PUSH_SUB(base_handle_init_pass)

    nullify(dict)
    call base_handle__init__(this, config)
    call json_get(config, "subsystems", dict, ierr)
    if(ierr==JSON_OK)then
      if(json_len(dict)>0)&
        call base_handle__init__(this, dict, handle_init)
    end if
    call base_handle__init__(this)
    nullify(dict)

    POP_SUB(base_handle_init_pass)
  end subroutine base_handle_init_pass

  ! ---------------------------------------------------------
  recursive subroutine base_handle_init_copy(this, that)
    type(base_handle_t), intent(out) :: this
    type(base_handle_t), intent(in)  :: that

    type(base_handle_iterator_t)        :: iter
    character(len=BASE_HANDLE_NAME_LEN) :: name
    type(base_handle_t),        pointer :: osub, isub
    integer                             :: ierr

    PUSH_SUB(base_handle_init_copy)

    call base_handle__init__(this, that)
    call base_handle_init(iter, that)
    do
      nullify(osub, isub)
      call base_handle_next(iter, name, isub, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call base_handle_new(this, osub)
      call base_handle_init(osub, isub)
      call base_handle_sets(this, name, osub)
    end do
    call base_handle_end(iter)
    call base_handle__init__(this)
    nullify(osub, isub)

    POP_SUB(base_handle_init_copy)
  end subroutine base_handle_init_copy

  ! ---------------------------------------------------------
  subroutine base_handle__build__(this)
    type(base_handle_t), intent(inout) :: this

    type(base_handle_iterator_t) :: iter
    type(base_handle_t), pointer :: subs
    integer                      :: ierr

    PUSH_SUB(base_handle__build__)

    call base_handle_init(iter, this)
    do
      nullify(subs)
      call base_handle_next(iter, subs, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call simulation_extend(this%sim, subs%sim, subs%config)
    end do
    call base_handle_end(iter)
    nullify(subs)

    POP_SUB(base_handle__build__)
  end subroutine base_handle__build__

  ! ---------------------------------------------------------
  subroutine base_handle__start__(this, grid)
    type(base_handle_t),    intent(inout) :: this
    type(grid_t), optional, intent(in)    :: grid

    PUSH_SUB(base_handle__start__)

    ASSERT(associated(this%config))
    call base_handle__build__(this)
    call simulation_start(this%sim, grid)
    ASSERT(simulation_assoc(this%sim))
    call base_model__start__(this%model, this%sim)

    POP_SUB(base_handle__start__)
  end subroutine base_handle__start__

  ! ---------------------------------------------------------
  recursive subroutine base_handle_start(this, grid)
    type(base_handle_t),    intent(inout) :: this
    type(grid_t), optional, intent(in)    :: grid

    type(base_handle_iterator_t) :: iter
    type(base_handle_t), pointer :: subs
    integer                      :: ierr

    PUSH_SUB(base_handle_start)

    call base_handle_init(iter, this)
    do
      nullify(subs)
      call base_handle_next(iter, subs, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call base_handle_start(subs, grid)
    end do
    call base_handle_end(iter)
    nullify(subs)
    call base_handle__start__(this, grid)

    POP_SUB(base_handle_start)
  end subroutine base_handle_start

  ! ---------------------------------------------------------
  subroutine base_handle__update__(this)
    type(base_handle_t), intent(inout) :: this

    PUSH_SUB(base_handle__update__)

    ASSERT(associated(this%config))
    call base_model__update__(this%model)

    POP_SUB(base_handle__update__)
  end subroutine base_handle__update__

  ! ---------------------------------------------------------
  recursive subroutine base_handle_update(this)
    type(base_handle_t), intent(inout) :: this

    type(base_handle_iterator_t) :: iter
    type(base_handle_t), pointer :: subs
    integer                      :: ierr

    PUSH_SUB(base_handle_update)

    call base_handle_init(iter, this)
    do
      nullify(subs)
      call base_handle_next(iter, subs, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call base_handle_update(subs)
    end do
    call base_handle_end(iter)
    nullify(subs)
    call base_handle__update__(this)

    POP_SUB(base_handle_update)
  end subroutine base_handle_update

  ! ---------------------------------------------------------
  subroutine base_handle__stop__(this)
    type(base_handle_t), intent(inout) :: this

    PUSH_SUB(base_handle__stop__)

    call base_model__stop__(this%model)

    POP_SUB(base_handle__stop__)
  end subroutine base_handle__stop__

  ! ---------------------------------------------------------
  recursive subroutine base_handle_stop(this)
    type(base_handle_t), intent(inout) :: this

    type(base_handle_iterator_t) :: iter
    type(base_handle_t), pointer :: subs
    integer                      :: ierr

    PUSH_SUB(base_handle_stop)

    call base_handle_init(iter, this)
    do
      nullify(subs)
      call base_handle_next(iter, subs, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call base_handle_stop(subs)
    end do
    call base_handle_end(iter)
    nullify(subs)
    call base_handle__stop__(this)

    POP_SUB(base_handle_stop)
  end subroutine base_handle_stop

  ! ---------------------------------------------------------
  subroutine base_handle__reset__(this)
    type(base_handle_t), intent(inout) :: this

    PUSH_SUB(base_handle__reset__)

    call base_model__reset__(this%model)

    POP_SUB(base_handle__reset__)
  end subroutine base_handle__reset__

  ! ---------------------------------------------------------
  subroutine base_handle__set__(this, that)
    type(base_handle_t),         intent(inout) :: this
    type(base_handle_t), target, intent(in)    :: that

    PUSH_SUB(base_handle__set__)

    this%prnt => that

    POP_SUB(base_handle__set__)
  end subroutine base_handle__set__

  ! ---------------------------------------------------------
  subroutine base_handle__sets__(this, name, that)
    type(base_handle_t), intent(inout) :: this
    character(len=*),    intent(in)    :: name
    type(base_handle_t), intent(in)    :: that

    PUSH_SUB(base_handle__sets__)

    call base_model_sets(this%model, name, that%model)

    POP_SUB(base_handle__sets__)
  end subroutine base_handle__sets__

  ! ---------------------------------------------------------
  subroutine base_handle_sets(this, name, that)
    type(base_handle_t), intent(inout) :: this
    character(len=*),    intent(in)    :: name
    type(base_handle_t), intent(in)    :: that

    PUSH_SUB(base_handle_sets)

    ASSERT(associated(this%config))
    call base_handle_dict_set(this%dict, trim(adjustl(name)), that)
    call base_handle__sets__(this, name, that)

    POP_SUB(base_handle_sets)
  end subroutine base_handle_sets

  ! ---------------------------------------------------------
  subroutine base_handle_gets_name(this, name, that)
    type(base_handle_t),  intent(in) :: this
    character(len=*),     intent(in) :: name
    type(base_handle_t), pointer     :: that

    PUSH_SUB(base_handle_gets_name)

    nullify(that)
    ASSERT(associated(this%config))
    call base_handle_dict_get(this%dict, trim(adjustl(name)), that)

    POP_SUB(base_handle_gets_name)
  end subroutine base_handle_gets_name

  ! ---------------------------------------------------------
  subroutine base_handle_get_info(this, type)
    type(base_handle_t), intent(in)  :: this
    integer,  optional,  intent(out) :: type

    PUSH_SUB(base_handle_get_info)

    if(present(type)) type = this%type

    POP_SUB(base_handle_get_info)
  end subroutine base_handle_get_info

  ! ---------------------------------------------------------
  subroutine base_handle_get_config(this, that)
    type(base_handle_t),  target, intent(in) :: this
    type(json_object_t), pointer             :: that

    PUSH_SUB(base_handle_get_config)

    nullify(that)
    if(associated(this%config)) that => this%config

    POP_SUB(base_handle_get_config)
  end subroutine base_handle_get_config

  ! ---------------------------------------------------------
  subroutine base_handle_get_simulation(this, that)
    type(base_handle_t), target, intent(in) :: this
    type(simulation_t), pointer             :: that

    PUSH_SUB(base_handle_get_simulation)

    that => this%sim

    POP_SUB(base_handle_get_simulation)
  end subroutine base_handle_get_simulation

  ! ---------------------------------------------------------
  subroutine base_handle_get_geom(this, that)
    type(base_handle_t),    intent(in) :: this
    type(base_geometry_t), pointer     :: that

    PUSH_SUB(base_handle_get_geom)

    call base_model_get(this%model, that)
    
    POP_SUB(base_handle_get_geom)
  end subroutine base_handle_get_geom

  ! ---------------------------------------------------------
  subroutine base_handle_get_density(this, that)
    type(base_handle_t),   intent(in) :: this
    type(base_density_t), pointer     :: that

    PUSH_SUB(base_handle_get_density)

    call base_model_get(this%model, that)
    
    POP_SUB(base_handle_get_density)
  end subroutine base_handle_get_density

  ! ---------------------------------------------------------
  subroutine base_handle_get_states(this, that)
    type(base_handle_t),  intent(in) :: this
    type(base_states_t), pointer     :: that

    PUSH_SUB(base_handle_get_states)

    call base_model_get(this%model, that)
    
    POP_SUB(base_handle_get_states)
  end subroutine base_handle_get_states

  ! ---------------------------------------------------------
  subroutine base_handle_get_system(this, that)
    type(base_handle_t),  intent(in) :: this
    type(base_system_t), pointer     :: that

    PUSH_SUB(base_handle_get_system)

    call base_model_get(this%model, that)
    
    POP_SUB(base_handle_get_system)
  end subroutine base_handle_get_system

  ! ---------------------------------------------------------
  subroutine base_handle_get_hamiltonian(this, that)
    type(base_handle_t),       intent(in) :: this
    type(base_hamiltonian_t), pointer     :: that

    PUSH_SUB(base_handle_get_hamiltonian)

    call base_model_get(this%model, that)
    
    POP_SUB(base_handle_get_hamiltonian)
  end subroutine base_handle_get_hamiltonian

  ! ---------------------------------------------------------
  subroutine base_handle_get_model(this, that)
    type(base_handle_t), target, intent(in) :: this
    type(base_model_t), pointer             :: that

    PUSH_SUB(base_handle_get_model)

    that => this%model

    POP_SUB(base_handle_get_model)
  end subroutine base_handle_get_model

  ! ---------------------------------------------------------
  subroutine base_handle__copy__begin(this, that)
    type(base_handle_t), intent(inout) :: this
    type(base_handle_t), intent(in)    :: that

    PUSH_SUB(base_handle__copy__begin)

    call base_handle__end__(this)
    if(associated(that%config))then
      call base_handle__init__(this, that)
      if(simulation_assoc(that%sim)) call base_model__copy__(this%model, that%model)
    end if

    POP_SUB(base_handle__copy__begin)
  end subroutine base_handle__copy__begin

  ! ---------------------------------------------------------
  subroutine base_handle__copy__finish(this)
    type(base_handle_t), intent(inout) :: this

    PUSH_SUB(base_handle__copy__finish)

    call base_model__copy__(this%model)

    POP_SUB(base_handle__copy__finish)
  end subroutine base_handle__copy__finish

  ! ---------------------------------------------------------
  recursive subroutine base_handle_copy_type(this, that)
    type(base_handle_t), intent(inout) :: this
    type(base_handle_t), intent(in)    :: that

    type(base_handle_iterator_t)        :: iter
    character(len=BASE_HANDLE_NAME_LEN) :: name
    type(base_handle_t),        pointer :: osub, isub
    integer                             :: ierr

    PUSH_SUB(base_handle_copy_type)

    nullify(osub, isub)
    call base_handle_end(this)
    call base_handle__copy__(this, that)
    call base_handle_init(iter, that)
    do
      nullify(osub, isub)
      call base_handle_next(iter, name, isub, ierr)
      if(ierr/=BASE_HANDLE_OK)exit
      call base_handle_new(this, osub)
      call base_handle_copy(osub, isub)
      call base_handle_sets(this, name, osub)
    end do
    call base_handle_end(iter)
    call base_handle__copy__(this)
    nullify(osub, isub)

    POP_SUB(base_handle_copy_type)
  end subroutine base_handle_copy_type

  ! ---------------------------------------------------------
  subroutine base_handle__end__(this)
    type(base_handle_t), intent(inout) :: this

    PUSH_SUB(base_handle__end__)

    nullify(this%config, this%prnt)
    this%type = HNDL_TYPE_NONE
    call simulation_end(this%sim)
    call base_model__end__(this%model)
    call base_handle_dict_end(this%dict)
    call base_handle_list_end(this%list)

    POP_SUB(base_handle__end__)
  end subroutine base_handle__end__

  ! ---------------------------------------------------------
  recursive subroutine base_handle_end_type(this)
    type(base_handle_t), intent(inout) :: this

    type(base_handle_t), pointer :: subs

    PUSH_SUB(base_handle_end_type)

    do
      nullify(subs)
      call base_handle_list_pop(this%list, subs)
      if(.not.associated(subs))exit
      call base_handle_end(subs)
      call base_handle__del__(subs)
    end do
    nullify(subs)
    call base_handle__end__(this)

    POP_SUB(base_handle_end_type)
  end subroutine base_handle_end_type

#define TEMPLATE_PREFIX base_handle
#define INCLUDE_BODY
#include "iterator_inc.F90"
#undef INCLUDE_BODY
#undef TEMPLATE_PREFIX

end module base_handle_oct_m

!! Local Variables:
!! mode: f90
!! End:
