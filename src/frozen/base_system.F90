#include "global.h"
 
#undef HASH_TEMPLATE_NAME
#undef HASH_KEY_TEMPLATE_NAME
#undef HASH_KEY_TYPE_NAME
#undef HASH_KEY_TYPE_MODULE_NAME
#undef HASH_KEY_FUNCTION_NAME
#undef HASH_KEY_FUNCTION_MODULE_NAME
#undef HASH_VAL_TEMPLATE_NAME
#undef HASH_VAL_TYPE_NAME
#undef HASH_VAL_TYPE_MODULE_NAME
#undef HASH_INCLUDE_PREFIX
#undef HASH_INCLUDE_HEADER
#undef HASH_INCLUDE_BODY

#define HASH_TEMPLATE_NAME base_system
#define HASH_KEY_TEMPLATE_NAME json
#define HASH_KEY_TYPE_NAME json_object_t
#define HASH_VAL_TEMPLATE_NAME base_system

#define HASH_INCLUDE_PREFIX
#include "thash.F90"
#undef HASH_INCLUDE_PREFIX

module base_system_m

  use global_m
  use messages_m
  use profiling_m

  use json_m,   only: operator(==), json_hash
  use kinds_m,  only: wp

  use json_m,  only: JSON_OK, json_object_t, json_get

  use config_dict_m, only: &
    CONFIG_DICT_OK,        &
    CONFIG_DICT_NAME_LEN

  use config_dict_m, only: &
    config_dict_t,         &
    config_dict_init,      &
    config_dict_set,       &
    config_dict_get,       &
    config_dict_copy,      &
    config_dict_end

  use simulation_m, only: &
    simulation_t

  use space_m, only: &
    operator(==),    &
    space_t,         &
    space_init,      &
    space_copy,      &
    space_end

  use geometry_m, only: &
    geometry_t

  use base_geom_m, only: &
    base_geom__init__,   &
    base_geom__add__

  use base_geom_m, only: &
    base_geom_t,         &
    base_geom_get,       &
    base_geom_copy,      &
    base_geom_end

  use base_states_m, only: &
    base_states__start__,  &
    base_states__update__, &
    base_states__stop__,   &
    base_states__add__

  use base_states_m, only: &
    base_states_t,         &
    base_states_init,      &
    base_states_get,       &
    base_states_copy,      &
    base_states_end

  implicit none

  private
  public ::                &
    base_system__init__,   &
    base_system__start__,  &
    base_system__update__, &
    base_system__stop__,   &
    base_system__add__,    &
    base_system__get__

  public ::             &
    base_system_init,   &
    base_system_start,  &
    base_system_update, &
    base_system_stop,   &
    base_system_next,   &
    base_system_set,    &
    base_system_get,    &
    base_system_copy,   &
    base_system_end

#define HASH_INCLUDE_HEADER
#include "thash.F90"
#undef HASH_INCLUDE_HEADER

  type, public :: base_system_t
    private
    type(json_object_t), pointer :: config =>null()
    type(simulation_t),  pointer :: sim    =>null()
    type(space_t)                :: space
    type(base_geom_t)            :: geom
    type(base_states_t)          :: st
    type(config_dict_t)          :: dict
    type(base_system_hash_t)     :: hash
  end type base_system_t

  type, public :: base_system_iterator_t
    private
    type(base_system_t),      pointer :: self =>null()
    type(base_system_hash_iterator_t) :: iter
  end type base_system_iterator_t

  interface base_system__init__
    module procedure base_system__init__begin
    module procedure base_system__init__finish
 end interface base_system__init__

  interface base_system_init
    module procedure base_system_init_system
    module procedure base_system_iterator_init
  end interface base_system_init

  interface base_system_next
    module procedure base_system_iterator_next_config_system
    module procedure base_system_iterator_next_config
    module procedure base_system_iterator_next_system
  end interface base_system_next

  interface base_system_set
    module procedure base_system_set_simulation
  end interface base_system_set

  interface base_system_get
    module procedure base_system_get_config
    module procedure base_system_get_simulation
    module procedure base_system_get_space
    module procedure base_system_get_geom
    module procedure base_system_get_states
  end interface base_system_get

  interface base_system_copy
    module procedure base_system_copy_system
    module procedure base_system_iterator_copy
  end interface base_system_copy

  interface base_system_end
    module procedure base_system_end_system
    module procedure base_system_iterator_end
  end interface base_system_end

  integer, public, parameter :: BASE_SYSTEM_OK          = BASE_SYSTEM_HASH_OK
  integer, public, parameter :: BASE_SYSTEM_KEY_ERROR   = BASE_SYSTEM_HASH_KEY_ERROR
  integer, public, parameter :: BASE_SYSTEM_EMPTY_ERROR = BASE_SYSTEM_HASH_EMPTY_ERROR

contains

#define HASH_INCLUDE_BODY
#include "thash.F90"
#undef HASH_INCLUDE_BODY

  ! ---------------------------------------------------------
  subroutine base_system__init__begin(this, config)
    type(base_system_t),         intent(out) :: this
    type(json_object_t), target, intent(in)  :: config
    !
    type(json_object_t), pointer :: cnfg
    integer                      :: ierr
    !
    PUSH_SUB(base_system__init__begin)
    this%config=>config
    nullify(this%sim, cnfg)
    call json_get(this%config, "space", cnfg, ierr)
    if(ierr==JSON_OK)call space_init(this%space, cnfg)
    nullify(cnfg)
    call json_get(this%config, "geometry", cnfg, ierr)
    if(ierr==JSON_OK)call base_geom__init__(this%geom, this%space, cnfg)
    nullify(cnfg)
    call json_get(this%config, "states", cnfg, ierr)
    if(ierr==JSON_OK)call base_states_init(this%st, cnfg)
    nullify(cnfg)
    call config_dict_init(this%dict)
    call base_system_hash_init(this%hash)
    POP_SUB(base_system__init__begin)
    return
  end subroutine base_system__init__begin

  ! ---------------------------------------------------------
  subroutine base_system__init__finish(this)
    type(base_system_t), intent(inout) :: this
    !
    PUSH_SUB(base_system__init__finish)
    call base_geom__init__(this%geom)
    POP_SUB(base_system__init__finish)
    return
  end subroutine base_system__init__finish

  ! ---------------------------------------------------------
  subroutine base_system_init_system(this, config)
    type(base_system_t), intent(out) :: this
    type(json_object_t), intent(in)  :: config
    !
    PUSH_SUB(base_system_init_system)
    call base_system__init__begin(this, config)
    call base_system__init__finish(this)
    POP_SUB(base_system_init_system)
    return
  end subroutine base_system_init_system

  ! ---------------------------------------------------------
  subroutine base_system__start__(this, sim)
    type(base_system_t),        intent(inout) :: this
    type(simulation_t), target, intent(in)    :: sim
    !
    PUSH_SUB(base_system__start__)
    ASSERT(associated(this%config))
    ASSERT(.not.associated(this%sim))
    this%sim=>sim
    call base_states__start__(this%st, sim)
    POP_SUB(base_system__start__)
    return
  end subroutine base_system__start__

  ! ---------------------------------------------------------
  recursive subroutine base_system_start(this, sim)
    type(base_system_t), intent(inout) :: this
    type(simulation_t),  intent(in)    :: sim
    !
    type(base_system_iterator_t) :: iter
    type(base_system_t), pointer :: subs
    integer                      :: ierr
    !
    PUSH_SUB(base_system_start)
    nullify(subs)
    call base_system_init(iter, this)
    do
      nullify(subs)
      call base_system_next(iter, subs, ierr)
      if(ierr/=BASE_SYSTEM_OK)exit
      call base_system_start(subs, sim)
    end do
    call base_system_end(iter)
    nullify(subs)
    call base_system__start__(this, sim)
    POP_SUB(base_system_start)
    return
  end subroutine base_system_start

  ! ---------------------------------------------------------
  subroutine base_system__update__(this)
    type(base_system_t), intent(inout) :: this
    !
    PUSH_SUB(base_system__update__)
    ASSERT(associated(this%config))
    ASSERT(associated(this%sim))
    call base_states__update__(this%st)
    POP_SUB(base_system__update__)
    return
  end subroutine base_system__update__

  ! ---------------------------------------------------------
  recursive subroutine base_system_update(this)
    type(base_system_t), intent(inout) :: this
    !
    type(base_system_iterator_t) :: iter
    type(base_system_t), pointer :: subs
    integer                      :: ierr
    !
    PUSH_SUB(base_system_update)
    nullify(subs)
    call base_system_init(iter, this)
    do
      nullify(subs)
      call base_system_next(iter, subs, ierr)
      if(ierr/=BASE_SYSTEM_OK)exit
      call base_system_update(subs)
    end do
    call base_system_end(iter)
    nullify(subs)
    call base_system__update__(this)
    POP_SUB(base_system_update)
    return
  end subroutine base_system_update

  ! ---------------------------------------------------------
  subroutine base_system__stop__(this)
    type(base_system_t), intent(inout) :: this
    !
    PUSH_SUB(base_system__stop__)
    ASSERT(associated(this%config))
    ASSERT(associated(this%sim))
    call base_states__stop__(this%st)
    POP_SUB(base_system__stop__)
    return
  end subroutine base_system__stop__

  ! ---------------------------------------------------------
  recursive subroutine base_system_stop(this)
    type(base_system_t), intent(inout) :: this
    !
    type(base_system_iterator_t) :: iter
    type(base_system_t), pointer :: subs
    integer                      :: ierr
    !
    PUSH_SUB(base_system_stop)
    nullify(subs)
    call base_system_init(iter, this)
    do
      nullify(subs)
      call base_system_next(iter, subs, ierr)
      if(ierr/=BASE_SYSTEM_OK)exit
      call base_system_stop(subs)
    end do
    call base_system_end(iter)
    nullify(subs)
    call base_system__stop__(this)
    POP_SUB(base_system_stop)
    return
  end subroutine base_system_stop

  ! ---------------------------------------------------------
  subroutine base_system__add__(this, that, config)
    type(base_system_t), intent(inout) :: this
    type(base_system_t), intent(in)    :: that
    type(json_object_t), intent(in)    :: config
    !
    character(len=CONFIG_DICT_NAME_LEN) :: name
    integer                             :: ierr
    !
    PUSH_SUB(base_system__add__)
    ASSERT(this%space==that%space)
    call json_get(config, "name", name, ierr)
    ASSERT(ierr==JSON_OK)
    call config_dict_set(this%dict, trim(adjustl(name)), config)
    call base_system_hash_set(this%hash, config, that)
    call base_geom__add__(this%geom, that%geom, config)
    call base_states__add__(this%st, that%st, config)
    POP_SUB(base_system__add__)
    return
  end subroutine base_system__add__

  ! ---------------------------------------------------------
  subroutine base_system__get__(this, name, that)
    type(base_system_t),  intent(inout) :: this
    character(len=*),    intent(in)    :: name
    type(base_system_t), pointer        :: that
    !
    type(json_object_t), pointer :: config
    integer                      :: ierr
    !
    PUSH_SUB(base_system__get__)
    nullify(that)
    call config_dict_get(this%dict, trim(adjustl(name)), config, ierr)
    if(ierr==CONFIG_DICT_OK)then
      call base_system_hash_get(this%hash, config, that, ierr)
      if(ierr/=BASE_SYSTEM_OK)nullify(that)
    end if
    POP_SUB(base_system__get__)
    return
  end subroutine base_system__get__

  ! ---------------------------------------------------------
  subroutine base_system_set_simulation(this, that)
    type(base_system_t),        intent(inout) :: this
    type(simulation_t), target, intent(in)    :: that
    !
    PUSH_SUB(base_system_set_simulation)
    ASSERT(.not.associated(this%sim))
    this%sim=>that
    POP_SUB(base_system_set_simulation)
    return
  end subroutine base_system_set_simulation

  ! ---------------------------------------------------------
  subroutine base_system_get_config(this, that)
    type(base_system_t),  target, intent(in) :: this
    type(json_object_t), pointer             :: that
    !
    PUSH_SUB(base_system_get_config)
    nullify(that)
    if(associated(this%config))&
      that=>this%config
    POP_SUB(base_system_get_config)
    return
  end subroutine base_system_get_config

  ! ---------------------------------------------------------
  subroutine base_system_get_simulation(this, that)
    type(base_system_t), target, intent(in) :: this
    type(simulation_t), pointer             :: that
    !
    PUSH_SUB(base_system_get_simulation)
    nullify(that)
    if(associated(this%sim))&
      that=>this%sim
    POP_SUB(base_system_get_simulation)
    return
  end subroutine base_system_get_simulation

  ! ---------------------------------------------------------
  subroutine base_system_get_space(this, that)
    type(base_system_t), target, intent(in) :: this
    type(space_t),      pointer             :: that
    !
    PUSH_SUB(base_system_get_space)
    that=>this%space
    POP_SUB(base_system_get_space)
    return
  end subroutine base_system_get_space

  ! ---------------------------------------------------------
  subroutine base_system_get_geom(this, that)
    type(base_system_t), target, intent(in) :: this
    type(base_geom_t),  pointer             :: that
    !
    PUSH_SUB(base_system_get_geom)
    that=>this%geom
    POP_SUB(base_system_get_geom)
    return
  end subroutine base_system_get_geom

  ! ---------------------------------------------------------
  subroutine base_system_get_states(this, that)
    type(base_system_t),  target, intent(in) :: this
    type(base_states_t), pointer             :: that
    !
    PUSH_SUB(base_system_get_states)
    that=>this%st
    POP_SUB(base_system_get_states)
    return
  end subroutine base_system_get_states

  ! ---------------------------------------------------------
  subroutine base_system_copy_system(this, that)
    type(base_system_t), intent(inout) :: this
    type(base_system_t), intent(in)    :: that
    !
    PUSH_SUB(base_system__copy___system)
    this%config=>that%config
    this%sim=>that%sim
    call base_geom_copy(this%geom, that%geom)
    call space_copy(this%space, that%space)
    call base_states_copy(this%st, that%st)
    call config_dict_copy(this%dict, that%dict)
    call base_system_hash_copy(this%hash, that%hash)
    POP_SUB(base_system__copy_system)
    return
  end subroutine base_system_copy_system

  ! ---------------------------------------------------------
  subroutine base_system_end_system(this)
    type(base_system_t), intent(inout) :: this
    !
    PUSH_SUB(base_system_end_system)
    nullify(this%config, this%sim)
    call space_end(this%space)
    call base_geom_end(this%geom)
    call base_states_end(this%st)
    call config_dict_end(this%dict)
    call base_system_hash_end(this%hash)
    POP_SUB(base_system_end_system)
    return
  end subroutine base_system_end_system

  ! ---------------------------------------------------------
  subroutine base_system_iterator_init(this, that)
    type(base_system_iterator_t), intent(out) :: this
    type(base_system_t),  target, intent(in)  :: that
    !
    PUSH_SUB(base_system_iterator_init)
    this%self=>that
    call base_system_hash_init(this%iter, that%hash)
    POP_SUB(base_system_iterator_init)
    return
  end subroutine base_system_iterator_init

  ! ---------------------------------------------------------
  subroutine base_system_iterator_next_config_system(this, config, system, ierr)
    type(base_system_iterator_t), intent(inout) :: this
    type(json_object_t),         pointer        :: config
    type(base_system_t),         pointer        :: system
    integer,            optional, intent(out)   :: ierr
    !
    PUSH_SUB(base_system_iterator_next_config_system)
    call base_system_hash_next(this%iter, config, system, ierr)
    POP_SUB(base_system_iterator_next_config_system)
    return
  end subroutine base_system_iterator_next_config_system

  ! ---------------------------------------------------------
  subroutine base_system_iterator_next_config(this, that, ierr)
    type(base_system_iterator_t), intent(inout) :: this
    type(json_object_t),         pointer        :: that
    integer,            optional, intent(out)   :: ierr
    !
    PUSH_SUB(base_system_iterator_next_config)
    call base_system_hash_next(this%iter, that, ierr)
    POP_SUB(base_system_iterator_next_config)
    return
  end subroutine base_system_iterator_next_config

  ! ---------------------------------------------------------
  subroutine base_system_iterator_next_system(this, that, ierr)
    type(base_system_iterator_t), intent(inout) :: this
    type(base_system_t),         pointer        :: that
    integer,            optional, intent(out)   :: ierr
    !
    PUSH_SUB(base_system_iterator_next_system)
    call base_system_hash_next(this%iter, that, ierr)
    POP_SUB(base_system_iterator_next_system)
    return
  end subroutine base_system_iterator_next_system

  ! ---------------------------------------------------------
  subroutine base_system_iterator_copy(this, that)
    type(base_system_iterator_t), intent(inout) :: this
    type(base_system_iterator_t), intent(in)    :: that
    !
    PUSH_SUB(base_system_iterator_copy)
    this%self=>that%self
    call base_system_hash_copy(this%iter, that%iter)
    POP_SUB(base_system_iterator_copy)
    return
  end subroutine base_system_iterator_copy

  ! ---------------------------------------------------------
  subroutine base_system_iterator_end(this)
    type(base_system_iterator_t), intent(inout) :: this
    !
    PUSH_SUB(base_system_iterator_end)
    nullify(this%self)
    call base_system_hash_end(this%iter)
    POP_SUB(base_system_iterator_end)
    return
  end subroutine base_system_iterator_end

end module base_system_m

#undef HASH_TEMPLATE_NAME
#undef HASH_KEY_TEMPLATE_NAME
#undef HASH_KEY_TYPE_NAME
#undef HASH_VAL_TEMPLATE_NAME

!! Local Variables:
!! mode: f90
!! End:
